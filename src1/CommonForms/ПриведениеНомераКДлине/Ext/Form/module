

// Разбирает строку, выделяя из нее префикс и числовую часть.
//
// Параметры:
//  Стр            - Строка. Разбираемая строка;
//  ЧисловаяЧасть  - Число. Переменная, в которую возвратится числовая часть строки;
//  Режим          - Строка. Если "Число", то возвратит числовую часть, иначе - префикс.
//
// Возвращаемое значение:
//  Префикс строки
//
Функция стПолучитьПрефиксЧислоНомера(Знач Стр, ЧисловаяЧасть = "", Режим = "")

	Стр     = СокрЛП(Стр);
	Длина   = СтрДлина(Стр);
	
	Префикс = Стр;
	ЧисловаяЧасть = "";
	
	ТекущаяПозиция = Длина;
	Пока ТекущаяПозиция >= 1 Цикл
		
		ТекущийСимвол = Сред(Стр, ТекущаяПозиция, 1);
		
		Если (ТекущийСимвол <= "9"
				И ТекущийСимвол >= "0") Тогда
				
			ЧисловаяЧасть = ТекущийСимвол + ЧисловаяЧасть;		
			ТекущаяПозиция = ТекущаяПозиция - 1;	
			
		Иначе
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// строка числовой части уже сформирована, формируем префикс
	Если ТекущаяПозиция <> 0 Тогда
		Префикс = Сред(Стр, 1, ТекущаяПозиция);
	Иначе
		Префикс = "";
	КонецЕсли;
	
	Если ЧисловаяЧасть = "" Тогда
		ЧисловаяЧасть = 0;
	Иначе
		ЧисловаяЧасть = Число(ЧисловаяЧасть);
	КонецЕсли;
	
	Если Режим = "Число" Тогда
		Возврат ЧисловаяЧасть;
	Иначе
		Возврат Префикс;
	КонецЕсли;

КонецФункции // ПолучитьПрефиксЧислоНомера()

// Приводит номер (код) к требуемой длине. При этом выделяется префикс
// и числовая часть номера, остальное пространство между префиксом и
// номером заполняется нулями.
//
// Параметры:
//  Стр          - преобразовываемая строка;
//  Длина        - требуемая длина строки.
//
// Возвращаемое значение:
//  Строка       - код или номер, приведенная к требуемой длине.
// 
Функция стПривестиНомерКДлине(Знач Стр, Длина, ДобавлятьНулиЕслиДлинаНеМеньшеТекущейДлиныНомера = Истина, Префикс = "")

	Если ПустаяСтрока(Стр)
		ИЛИ СтрДлина(Стр) = Длина Тогда
		
		Возврат Стр;
		
	КонецЕсли;
	
	Стр             = СокрЛП(Стр);
	ВходящаяДлинаНомера = СтрДлина(Стр);

	ЧисловаяЧасть   = "";
	ПрефиксНомераСтроки   = стПолучитьПрефиксЧислоНомера(Стр, ЧисловаяЧасть);
	
	ИтоговыйПрефикс = ?(ПустаяСтрока(Префикс), ПрефиксНомераСтроки, Префикс);
	ДлинаИтоговогоПрефикса = СтрДлина(ИтоговыйПрефикс);
	
	СтрокаЧисловойЧасти = Формат(ЧисловаяЧасть, "ЧГ=0");
	ДлинаЧисловойЧасти = СтрДлина(СтрокаЧисловойЧасти);

	Если (Длина >= ВходящаяДлинаНомера И ДобавлятьНулиЕслиДлинаНеМеньшеТекущейДлиныНомера)
		ИЛИ (Длина < ВходящаяДлинаНомера) Тогда
		
		Для ВременнаяПеременная = 1 По Длина - ДлинаИтоговогоПрефикса - ДлинаЧисловойЧасти Цикл
			
			СтрокаЧисловойЧасти = "0" + СтрокаЧисловойЧасти;
			
		КонецЦикла;
	
	КонецЕсли;
	
	// обрезаем лишние символы
	СтрокаЧисловойЧасти = Прав(СтрокаЧисловойЧасти, Длина - ДлинаИтоговогоПрефикса);
		
	Результат = ИтоговыйПрефикс + СтрокаЧисловойЧасти;

	Возврат Результат;

КонецФункции // ПривестиНомерКДлине()

Процедура ПривестиНомер()
	
	РезультирующийНомер = стПривестиНомерКДлине(ИсходныйНомер, ДлинаНомераВПриемнике);
	
КонецПроцедуры

Процедура ОсновныеДействияФормыПривестиКДлине(Кнопка)
	
	ПривестиНомер();	
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(ДлинаСтрокиИсточника) Тогда
		ДлинаСтрокиИсточника = 12;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДлинаНомераВПриемнике) Тогда
		ДлинаНомераВПриемнике = 9;
	КонецЕсли;
	
	Если ПустаяСтрока(ИсходныйНомер) Тогда
		
		Если ДлинаСтрокиИсточника <= 3 Тогда
			ИсходныйНомер = "А12";
		ИначеЕсли ДлинаСтрокиИсточника <= 6 Тогда
			ИсходныйНомер = стПривестиНомерКДлине("А00123", ДлинаСтрокиИсточника);
		Иначе
			ИсходныйНомер = стПривестиНомерКДлине("ААА000000000000123", ДлинаСтрокиИсточника);
		КонецЕсли;
		
	КонецЕсли;
	
	ПривестиНомер();
	
КонецПроцедуры

Процедура ДлинаНомераВПриемникеПриИзменении(Элемент)
	
	Если ДлинаНомераВПриемнике <= 0 Тогда
		ДлинаНомераВПриемнике = 1;
	КонецЕсли;
	
КонецПроцедуры
