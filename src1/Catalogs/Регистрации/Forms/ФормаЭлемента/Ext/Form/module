////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если Не ЭтоНовый() Тогда
		
		ФайлПравилТекущегоПользователя = РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь);
		
	КонецЕсли;
	
	ЗаполнитьСписокВыбораПланаОбмена();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, ФайлПравилТекущегоПользователя);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ФОРМЫ

Процедура КонфигурацияПриИзменении(Элемент)
	
	ЗаполнитьСписокВыбораПланаОбмена();
	
КонецПроцедуры

Процедура ФайлПравилТекущегоПользователяНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	РаботаСФайлами.глВыборФайла(Элемент, ,"Файл данных (*.xml)|*.xml|Архивный файл данных (*.zip)|*.zip", , "Выбирете имя файла для сохранения правил");
	
КонецПроцедуры

Процедура ОсновныеДействияФормыПомощь(Кнопка)
	
	ПоказатьИнформациюОБыстромОсвоении("СозданиеПравилРегистрацииОбъектов");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ЗаполнитьСписокВыбораПланаОбмена()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СправочникОбъекты.Ссылка  КАК ПланОбменаСсылка,
	|	СправочникОбъекты.Синоним КАК ПланОбменаСиноним
	|ИЗ
	|	Справочник.Объекты КАК СправочникОбъекты
	|ГДЕ
	|	  СправочникОбъекты.Тип      = &Тип
	|	И СправочникОбъекты.Владелец = &Конфигурация
	|УПОРЯДОЧИТЬ ПО
	|	СправочникОбъекты.Синоним
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Тип",          Перечисления.ТипыОбъектов.ПланОбмена);
	Запрос.УстановитьПараметр("Конфигурация", Конфигурация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СписокВыбора = Новый СписокЗначений;
	
	Пока Выборка.Следующий() Цикл
		
		СписокВыбора.Добавить(Выборка.ПланОбменаСсылка, Выборка.ПланОбменаСиноним,, БиблиотекаКартинок.ПланОбмена);
		
	КонецЦикла;
	
	МодифицированностьДоОбновления = Модифицированность;
	
	ЭлементыФормы.ПланОбмена.СписокВыбора = СписокВыбора;
	
	ЭлементыФормы.ПланОбмена.Значение = ПланОбмена;
	
	Если Не МодифицированностьДоОбновления Тогда
		
		ЭтаФорма.Модифицированность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьНаименованиеНажатие(Элемент)
	
	Наименование = глНаименованиеРегистрации(ЭтотОбъект);
	
КонецПроцедуры

Процедура ФайлПравилТекущегоПользователяОткрытие(Элемент, СтандартнаяОбработка)
	
	РаботаСФайлами.глОткрытьФайл(Элемент.Значение, СтандартнаяОбработка);
	
КонецПроцедуры
