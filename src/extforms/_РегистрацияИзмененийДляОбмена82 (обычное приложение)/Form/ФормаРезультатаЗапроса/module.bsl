Перем мФормаПараметров;
Перем мРезультатЗапроса;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	мФормаПараметров = ЭтотОбъект.ПолучитьФорму("ФормаПараметров", ЭтаФорма);
	мРезультатЗапроса = Неопределено;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

Процедура КнопкаОКНажатие(Кнопка)
	
	КолонкаСсылки = РезультатТаблица.Колонки.Найти("Ссылка");
	
	Если КолонкаСсылки = Неопределено Тогда
		
		Предупреждение("Не найдена колонка ""Ссылка""");
		Возврат;
		
	КонецЕсли;
	
	Закрыть(Истина);
	
КонецПроцедуры

Процедура КоманднаяПанель1Выполнить(Кнопка)
	
	ОбъектЗапрос = Новый Запрос;
	
	Для каждого СтрокаПараметров Из мФормаПараметров.Параметры Цикл
		Если СтрокаПараметров.ЭтоВыражение Тогда
			ОбъектЗапрос.УстановитьПараметр(СтрокаПараметров.ИмяПараметра, Вычислить(СтрокаПараметров.ЗначениеПараметра));
		Иначе
			ОбъектЗапрос.УстановитьПараметр(СтрокаПараметров.ИмяПараметра, СтрокаПараметров.ЗначениеПараметра);
		КонецЕсли;
	КонецЦикла;
	
	ОбъектЗапрос.Текст = СтрЗаменить(ПолучитьТекстЗапроса(Истина), "|", "");
	
	Если ПустаяСтрока(ОбъектЗапрос.Текст) Тогда
		Предупреждение("Не заполнен текст запроса!", 30);
		Возврат;
	КонецЕсли;
	
	мРезультатЗапроса = ОбъектЗапрос.Выполнить();
	
	ЗагрузитьРезультат();
	
КонецПроцедуры

Процедура КоманднаяПанель1Очистить(Кнопка)
	
	мРезультатЗапроса = Неопределено;
	
	РезультатТаблица.Очистить();
	
КонецПроцедуры

Процедура КоманднаяПанель1Параметры(Кнопка)
	ТекстЗапроса = СтрЗаменить(ПолучитьТекстЗапроса(Истина), "|", ""); 
	
	Если ПустаяСтрока(ТекстЗапроса) Тогда
		Предупреждение("Не заполнен текст запроса!", 30);
		Возврат;
	КонецЕсли;
	
	Если мФормаПараметров.Открыта() = Истина Тогда
		мФормаПараметров.Закрыть();
	Иначе
		мФормаПараметров.Открыть();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Получает текст запроса из текстового поля
//
// Параметры:
//  СВыделением - признак получения только выделенного текста.
//
// Возвращаемое значение:
//	Текст запроса в виде строки.
//
Функция ПолучитьТекстЗапроса(СВыделением)

	Если Не СВыделением Тогда
		Возврат ЭлементыФормы.ТекстЗапроса.ПолучитьТекст();
	КонецЕсли;

    ТекстЗап = ЭлементыФормы.ТекстЗапроса.ПолучитьВыделенныйТекст();
	Если СтрДлина(ТекстЗап) <> 0 Тогда
		Возврат ТекстЗап;
	Иначе
		Возврат ЭлементыФормы.ТекстЗапроса.ПолучитьТекст();
	КонецЕсли;

КонецФункции // ПолучитьТекстЗапроса()

// Загружает результат запроса в таблицу или сводную таблицу
//
// Параметры:
//  Нет.
//
Процедура ЗагрузитьРезультат()
	
	Если мРезультатЗапроса <> Неопределено Тогда
		
		РезультатТаблица = мРезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.Прямой);
		ЭлементыФормы.ТаблицаРезультата.СоздатьКолонки();
		
	КонецЕсли;
	
КонецПроцедуры // ЗагрузитьРезультат()

