
#Если ТолстыйКлиентОбычноеПриложение Тогда

Процедура ОбщаяФормаПравилаОбменаПриОткрытии(ЭтаФорма) Экспорт
	
	КнопкиПодменю = ЭтаФорма.ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ОтладкаОбработчиков.Кнопки;
	КнопкиПодменю.Добавить("bnНастройкиГитфлоу", ТипКнопкиКоманднойПанели.Действие,
		"Адаптация под Git-flow...", Новый Действие("bnНастройкиГитфлоуНажатие"));
	
КонецПроцедуры

Процедура НастройкиГитфлоуНажатие(ЭтаФорма) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Конвертация) Тогда
		Предупреждение(НСтр("ru = 'Конвертация не записана. Запишите конвертацию и повторите операцию.'"));
		Возврат;
	КонецЕсли;
	
	ФормаНастроек = ПолучитьОбщуюФорму("bnНастройкиГитфлоу");
	ФормаНастроек.Конвертация = ЭтаФорма.Конвертация;
	ФормаНастроек.Открыть();
	
КонецПроцедуры

#КонецЕсли

Функция ПолучитьИзСтрокиТолькоЦифры(ИсходнаяСтрока) Экспорт
	
	КопияСтроки = СокрЛП(ИсходнаяСтрока);
	ДлинаСтроки = СтрДлина(КопияСтроки);
	Результат = "";
	
	Для Сч = 1 По ДлинаСтроки Цикл
		
		Симв = Сред(КопияСтроки, Сч, 1);
		Если Найти("0123456789", Симв) > 0 Тогда
			Результат = Результат + Симв;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ВернутьСтрокуВыгрузки_ДатаОбновления(ИсходнаяДата) Экспорт
	
	Результат = ИсходнаяДата;
	ОкруглениеДатыВыгрузки = bnГитфлоуПовтИсп.ПолучитьЗначениеНастройки("bnОкруглениеДатыВыгрузки");
	
	Если ОкруглениеДатыВыгрузки = 1 Тогда
		Результат = НачалоМинуты(ИсходнаяДата);
	ИначеЕсли ОкруглениеДатыВыгрузки = 2 Тогда
		Результат = НачалоЧаса(ИсходнаяДата);
	ИначеЕсли ОкруглениеДатыВыгрузки = 3 Тогда
		Результат = НачалоДня(ИсходнаяДата);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
