////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Перем мЭтоНоваяКонфигурация;

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()

	Если Не ЭтоНовый() Тогда
		ИмяФайлаТекущегоПользователя = РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь);
        Если РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь, "ТипРасположенияИБ") = "1" Тогда
			ТипРасположенияИБ = 1;
		Иначе
			ТипРасположенияИБ = 0;
		КонецЕсли;
		КаталогИБ    = РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь, "КаталогИБ");
		ИмяСервера   = РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь, "ИмяСервера");
		ИмяИБ        = РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь, "ИмяИБ");
        Если РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь, "Аутентификация") = "1" Тогда
			Аутентификация = 1;
		Иначе
			Аутентификация = 0;
		КонецЕсли;
		Пользователь      = РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь, "Пользователь");
		Пароль            = РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь, "Пароль");
		ИсполняемыйФайл77 = РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь, "ИсполняемыйФайл77");
		ИсполняемыйФайл80 = РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь, "ИсполняемыйФайл80");
		ВерсияПлатформы8  = РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь, "ВерсияПлатформы8");
		
		Если ПустаяСтрока(ВерсияПлатформы8) Тогда
		
			ВерсияПлатформы8 = "8.0";
			
		КонецЕсли;
		
	Иначе
		Приложение = Перечисления.Приложения.Предприятие80;
	КонецЕсли;

	Если Приложение = Перечисления.Приложения.Предприятие77 Тогда
		
		ЭлементыФормы.ВерсияПлатформы8.Видимость        = Ложь;
		ЭлементыФормы.НадписьВерсияПлатформы8.Видимость = Ложь;
		
		ЭлементыФормы.ТипРасположенияИБСерверная.Доступность = Ложь;
		ЭлементыФормы.АутентификацияWindows.Доступность      = Ложь;
		
	Иначе
		
		ЭлементыФормы.ИсполняемыйФайл77.Доступность = Ложь;
		
	КонецЕсли;
	
	ЭлементыФормы.ИсполняемыйФайл80.Доступность = НЕ ЭлементыФормы.ИсполняемыйФайл77.Доступность;

	ТипРасположенияИБПриИзменении(ЭтаФорма);
	АутентификацияПриИзменении(ЭтаФорма);
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ПриЗаписи" формы.
//
Процедура ПриЗаписи(Отказ)

	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, ИмяФайлаТекущегоПользователя);

	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, ТипРасположенияИБ,  "ТипРасположенияИБ");
	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, КаталогИБ,          "КаталогИБ");
	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, ИмяСервера,         "ИмяСервера");
	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, ИмяИБ,              "ИмяИБ");
    РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, Аутентификация,     "Аутентификация");
	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, Пользователь,       "Пользователь");
	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, Пароль,             "Пароль");
	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, ИсполняемыйФайл77,  "ИсполняемыйФайл77");
	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, ИсполняемыйФайл80,  "ИсполняемыйФайл80");
	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, ВерсияПлатформы8,   "ВерсияПлатформы8");
		
	// спросим у пользователя, может он хочет загрузить данные для новой конфигурации
	Если мЭтоНоваяКонфигурация Тогда
		
		// пользователю нужно задать вопрос о рекурсивном автоматическом создании ПКС, ПКЗ
		ОтветПользователя = Вопрос("Загрузить метаданные конфигурации из файла?", 
			РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
			
		Если ОтветПользователя <> КодВозвратаДиалога.Да Тогда	
		
			Возврат;
		
		КонецЕсли;
		
		ОткрытьОбработкуЗагрузкиМетаДанныхИзФайла();
		
	КонецЕсли;

КонецПроцедуры // ПриЗаписи()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНОЙ ПАНЕЛИ ДОКУМЕНТОВ ОБНОВЛЕНИЯ


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ

// Процедура - обработчик события "НачалоВыбора" поля ввода файла правил.
//
Процедура ФайлПравилТекущегоПользователяНачалоВыбора(Элемент, СтандартнаяОбработка)

	РаботаСФайлами.глВыборФайла(Элемент, Истина, , ,"Выбирете имя файла для сохранения правил");
	
КонецПроцедуры // ФайлПравилТекущегоПользователяНачалоВыбора()

// Процедура - обработчик события "НачалоВыбора" поля ввода каталога ИБ.
//
Процедура КаталогИБНачалоВыбора(Элемент, СтандартнаяОбработка)

	РаботаСФайлами.глВыборКаталога(Элемент, Истина);
	
КонецПроцедуры // КаталогИБНачалоВыбора()

Процедура ТипРасположенияИБПриИзменении(Элемент)

	ЭлементыФормы.ПанельПодключенияКИБ.ТекущаяСтраница = ?(ТипРасположенияИБ = 0, ЭлементыФормы.ПанельПодключенияКИБ.Страницы.Файловая, ЭлементыФормы.ПанельПодключенияКИБ.Страницы.Серверная);	
		
КонецПроцедуры // ТипРасположенияИБПриИзменении()

Процедура АутентификацияПриИзменении(Элемент)

	ЭлементыФормы.Пользователь.Доступность = Аутентификация = 0;
	ЭлементыФормы.Пароль.Доступность       = Аутентификация = 0;
		
КонецПроцедуры // КоманднаяПанельФормыДействия()

Процедура ИсполняемыйФайл77НачалоВыбора(Элемент, СтандартнаяОбработка)

   	ДиалогВыбораФайла =	Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Фильтр                      =	"Файл данных (*.exe)|*.exe";
	ДиалогВыбораФайла.Заголовок                   =	"Выберите исполняемый файл 1С:Предприятие";
	ДиалогВыбораФайла.ПредварительныйПросмотр     =	Ложь;
	ДиалогВыбораФайла.Расширение                  =	"exe";
	ДиалогВыбораФайла.ИндексФильтра               =	0;
	ДиалогВыбораФайла.ПолноеИмяФайла              =	Элемент.Значение;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла =	Истина;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		Элемент.Значение = ДиалогВыбораФайла.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "Открытие" поля ввода "ФайлПравилТекущегоПользователя"
//
Процедура ФайлПравилТекущегоПользователяОткрытие(Элемент, СтандартнаяОбработка)

	РаботаСФайлами.глОткрытьФайл(ИмяФайлаТекущегоПользователя, СтандартнаяОбработка);

КонецПроцедуры // ФайлПравилТекущегоПользователяОткрытие()

Процедура ОткрытьОбработкуЗагрузкиМетаДанныхИзФайла()
			
	Если НЕ ЗначениеЗаполнено(Ссылка)
		ИЛИ Модифицированность() Тогда
		
		Записать();
		Закрыть();		
			
	КонецЕсли;
	
	// загружаем описание конфигурации из файла
	Обработка = Обработки.ЗагрузкаСтруктурыМетаданных.Создать();
	Обработка.Конфигурация  = Ссылка;
	Обработка.ИмяФайлаЗагрузки = ИмяФайлаТекущегоПользователя;	
	
	ФормаОбработки = Обработка.ПолучитьФорму();
	ФормаОбработки.Открыть();
	
КонецПроцедуры

Процедура ЗагрузитьОписаниеКонфигурацииНажатие(Элемент)
	
	ОткрытьОбработкуЗагрузкиМетаДанныхИзФайла();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	мЭтоНоваяКонфигурация = ЭтоНовый();
	
КонецПроцедуры

Процедура ПросмотретьОписаниеКонфигурации1Нажатие(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		
		Предупреждение("Не загружены данные для просмотра.");
		Возврат;
		
	КонецЕсли;
	
	// загружаем описание конфигурации из файла
	ФормаПросмотра = ПолучитьОбщуюФорму("Конфигурация");
	ФормаПросмотра.Конфигурация  = Ссылка;
	ФормаПросмотра.Открыть();
	
КонецПроцедуры

Процедура ДействияФормыРежимРедактирования(Кнопка)
	
	ЭлементыФормы.Имя.ТолькоПросмотр = Ложь;
	ЭлементыФормы.Синоним.ТолькоПросмотр = Ложь;
	ЭлементыФормы.Комментарий.ТолькоПросмотр = Ложь;
	ЭлементыФормы.Приложение.ТолькоПросмотр = Ложь;
	ЭлементыФормы.Версия.ТолькоПросмотр = Ложь;
	
	ЭлементыФормы.Имя.РедактированиеТекста = Истина;
	ЭлементыФормы.Синоним.РедактированиеТекста = Истина;
	ЭлементыФормы.Комментарий.РедактированиеТекста = Истина;
	ЭлементыФормы.Приложение.РедактированиеТекста = Истина;
	ЭлементыФормы.Версия.РедактированиеТекста = Истина;
	
КонецПроцедуры
