////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

//Вспомогательная переменная (массив) для хранения обработчиков
Перем мМассивОбработчиков;

//{benony
Перем мТекстОбработчикаДоИзменений;
Перем мПутьКФайлуДоИзменений;
Перем мПутьКФайлуТекущий;
//}benony

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	Если ЭтоНовый() Тогда
		
		// значение по умолчанию для свойства СпособОтбораДанных
		Если Не ЗначениеЗаполнено(СпособОтбораДанных) Тогда
			
			СпособОтбораДанных = Перечисления.СпособыОтбораДанных.СтандартнаяВыборка;
			
		КонецЕсли;
		
		ТекущийЭлемент = ЭлементыФормы.ОбъектВыборки;
		
	Иначе
		
		ТекущийЭлемент = ЭлементыФормы.ПанельАлгоритмы;
		
	КонецЕсли;
			
КонецПроцедуры

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()

	ПолучитьАлгоритмыПриОткрытии(ЭтаФорма, ЭтотОбъект, мМассивОбработчиков);
	
	Конфигурация = Владелец.Источник;
	
	АктивизироватьСтраницуСобытия(ЭтотОбъект, мМассивОбработчиков, ЭлементыФормы.ПанельАлгоритмы, ЭлементыФормы);
	
	Если НЕ ЭтоПриложение8(ЭтотОбъект.Владелец.Источник.Приложение) Или НЕ ЭтоПриложение8(ЭтотОбъект.Владелец.Приемник.Приложение) Тогда
		ЭлементыФормы.КоманднаяПанель1.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанель1.Кнопки.ОтладкаОбработчиков);
	КонецЕсли;
	
	РедакторОбработчиковКлиент.УстановитьСостояниеКнопокПанелиРедактора(ЭлементыФормы, "ПриОткрытии"); //benony
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ПередЗаписью" формы.
//
Процедура ПередЗаписью(Отказ)
	
	УстановитьАлгоритмыПередЗаписью(ЭтаФорма, ЭтотОбъект, мМассивОбработчиков);
	
	ПроверитьВозможностьСозданияПКОДляОбъекта(Отказ, ОбъектВыборки);
	
КонецПроцедуры // ПередЗаписью()


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ

// Процедура - обработчик события "Регулирование" поля ввода порядка.
//
Процедура ПорядокРегулирование(Элемент, Направление, СтандартнаяОбработка)

	ИзменитьПорядокИнтерактивно(Элемент, Направление, СтандартнаяОбработка);
	
КонецПроцедуры

// Процедура - обработчик события "НачалоВыбораИзСписка" поля ввода порядка.
//
Процедура ПравилоКонвертацииНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)

	Элемент.СписокВыбора.Очистить();

	Элемент.СписокВыбора = НайтиПравилаКОПоКонвертацииИТИпуИсточника(ЭтотОбъект.Владелец, ЭтотОбъект.ОбъектВыборки);

КонецПроцедуры // ПравилоКонвертацииНачалоВыбораИзСписка()


// Процедура - обработчик события "ПриСменеСтраницы" панели закладок.
//
Процедура ПанельАлгоритмыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ОбновитьПиктограммыАлгоритмов(мМассивОбработчиков, ЭлементыФормы.ПанельАлгоритмы, ЭлементыФормы);
	
	ЗаполнитьИмяОбработчика(Элемент.ТекущаяСтраница);
	
КонецПроцедуры // ПанельАлгоритмыПриСменеСтраницы()

Процедура КоманднаяПанель1ИнформацияПоОбработчикам(Кнопка)
	
	// вывод информации по обработчикам событий
	ОткрытьОписаниеОбработчиковСобытий("ПравилаВыгрузкиДанных", ЭлементыФормы.ПанельАлгоритмы.ТекущаяСтраница.Имя, ЭтаФорма);
	
КонецПроцедуры

Процедура ОбъектВыборкиПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(ОбъектВыборки) Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбораПравилКонвертации = НайтиПравилаКОПоКонвертацииИТИпуИсточника(ЭтотОбъект.Владелец, ЭтотОбъект.ОбъектВыборки);
	
	КоличествоЭлементовВСписке = СписокВыбораПравилКонвертации.Количество();
	
	Если КоличествоЭлементовВСписке = 0 Тогда
		
		ПравилоКонвертации = Справочники.ПравилаКонвертацииОбъектов.ПустаяСсылка();
		
	ИначеЕсли КоличествоЭлементовВСписке = 1 Тогда
		
		ПравилоКонвертации = СписокВыбораПравилКонвертации[0].Значение;
		
	КонецЕсли;
	
	СгенерироватьУникальныйКод();
	
КонецПроцедуры

Процедура СформироватьНаименование1Нажатие(Элемент)
	
	УстановитьГруппуПоУмолчаниюДляПВД(ЭтотОбъект);
	
КонецПроцедуры

Процедура КоманднаяПанель1СкопироватьОбработчикиВыгрузки(Кнопка)
	
	ВыгрузитьКодОбработчиков(ЭтотОбъект.Владелец, Истина, ЭтотОбъект.Ссылка);
	
КонецПроцедуры

Процедура КоманднаяПанель1ВставитьОбработчики(Кнопка)
	
	ЗагрузитьКодОбработчиков(ЭтотОбъект.Владелец, ЭтаФорма, мМассивОбработчиков);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ЗаполнитьИмяОбработчика(ТекущаяСтраница)
	
	ИмяСобытия = Сред(ТекущаяСтраница.Имя, 9);
	ИмяОбработчика = "ПВД_[КодПВД]_[ИмяСобытия]";
	ИмяОбработчика = СтрЗаменить(ИмяОбработчика, "[КодПВД]", СокрЛП(ЭтотОбъект.Код));
	ИмяОбработчика = СтрЗаменить(ИмяОбработчика, "[ИмяСобытия]", ИмяСобытия);
	ЭлементыФормы["ИмяОбработчика"+ИмяСобытия].Значение = ИмяОбработчика;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// РЕДАКТИРОВАНИЕ АЛГОРИТМОВ ОБРАБОТЧИКОВ

//
//benony
Процедура КоманднаяПанельРедактированиеОбработчиковИзменить(Кнопка)
	
	РедакторОбработчиковКлиент.КомандаРедактораИзменить(ЭлементыФормы, мМассивОбработчиков, мТекстОбработчикаДоИзменений, мПутьКФайлуДоИзменений, мПутьКФайлуТекущий);
	
	ЗапуститьПриложение(мПутьКФайлуТекущий);
	ПодключитьОбработчикОжидания("ПроверитьРазличияВФайлах", 3);
	
КонецПроцедуры

//
//benony
Процедура КоманднаяПанельРедактированиеОбработчиковЗаписать(Кнопка)
	
	ОтключитьОбработчикОжидания("ПроверитьРазличияВФайлах");
	
	РедакторОбработчиковКлиент.КомандаРедактораЗаписать(ЭлементыФормы, мМассивОбработчиков, мПутьКФайлуТекущий);
	
КонецПроцедуры

//
//benony
Процедура КоманднаяПанельРедактированиеОбработчиковОтменитьИзменения(Кнопка)
	
	ОтключитьОбработчикОжидания("ПроверитьРазличияВФайлах");
	
	РедакторОбработчиковКлиент.КомандаРедактораОтменитьИзменения(ЭлементыФормы, мМассивОбработчиков, мТекстОбработчикаДоИзменений);
	
КонецПроцедуры

//
//benony
Процедура КоманднаяПанельРедактированиеОбработчиковПоказатьИзменения(Кнопка)
	
	РедакторОбработчиковКлиент.КомандаРедактораПоказатьРазличия(мПутьКФайлуДоИзменений, мПутьКФайлуТекущий);
	
КонецПроцедуры

//
//benony
Процедура ПроверитьРазличияВФайлах()
	
	РедакторОбработчиковКлиент.ОбработчикОжиданияПроверитьРазличия(ЭлементыФормы, мПутьКФайлуДоИзменений, мПутьКФайлуТекущий);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мМассивОбработчиков = ПолучитьМассивИменОбработчиков("ПравилаВыгрузкиДанных");
