
Перем мМассивПереходовПоСтраницам;
Перем мОсновнаяПанель;

Перем мМассивОбработчиков;


Процедура ПриЗакрытии()

	Если глТекущийПользователь.НеИспользоватьМастерНастроекДляПКО <> НеИспользоватьМастерНастроекДляПКО Тогда
		
		ТекущийПользователь = глТекущийПользователь.ПолучитьОбъект();
		ТекущийПользователь.НеИспользоватьМастерНастроекДляПКО = НеИспользоватьМастерНастроекДляПКО;
		ТекущийПользователь.Записать();
		
	КонецЕсли;

КонецПроцедуры

Процедура ПриОткрытии()
	
	НеИспользоватьМастерНастроекДляПКО = глТекущийПользователь.НеИспользоватьМастерНастроекДляПКО;
	
	КонфигурацияИсточник = Владелец.Источник;
	КонфигурацияПриемник = Владелец.Приемник;
	
	ПолучитьАлгоритмыПриОткрытии(ЭтаФорма, ЭтотОбъект, мМассивОбработчиков);
	
	ОпределитьДоступностьКнопокПереходов();
	
	Если НЕ ЭтоПриложение8(КонфигурацияИсточник.Приложение) Или НЕ ЭтоПриложение8(КонфигурацияПриемник.Приложение) Тогда
		ЭлементыФормы.ДействияФормы.Кнопки.Удалить(ЭлементыФормы.ДействияФормы.Кнопки.ОтладкаОбработчиков);
	КонецЕсли;
	
КонецПроцедуры

Процедура ИсточникПриИзменении(Элемент)
		
	ИсточникПриИзмененииПКО(ЭтотОбъект);	
	
КонецПроцедуры

Процедура ПриемникПриИзменении(Элемент)
	
	ПриемникПриИзмененииПКО(ЭтотОбъект);
		
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////

Процедура ВыполнитьПереходДалее()
	
	Если ЭлементыФормы.ПанельГлавная.ТекущаяСтраница = ЭлементыФормы.ПанельГлавная.Страницы.Основная Тогда
		
		Если Не ЗначениеЗаполнено(Приемник) Тогда
			
			Предупреждение("Не указан объект - приемник");
			Возврат;
			
		КонецЕсли;
		
		Если ЭтоНовый() Тогда
			ОпределитьНужнаСинхронизацияПоИдентификатору(ЭтотОбъект);
		КонецЕсли;
		
		мМассивПереходовПоСтраницам.Добавить(мОсновнаяПанель.ТекущаяСтраница);
		ЭлементыФормы.ПанельГлавная.ТекущаяСтраница = ЭлементыФормы.ПанельГлавная.Страницы.ДополнительныеПараметры;
		
	ИначеЕсли ЭлементыФормы.ПанельГлавная.ТекущаяСтраница = ЭлементыФормы.ПанельГлавная.Страницы.ДополнительныеПараметры Тогда
		
		мМассивПереходовПоСтраницам.Добавить(мОсновнаяПанель.ТекущаяСтраница);
		ЭлементыФормы.ПанельГлавная.ТекущаяСтраница = ЭлементыФормы.ПанельГлавная.Страницы.События;	
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ВыполнитьПереходНазад()
	
	КоличествоСтраницИстории = мМассивПереходовПоСтраницам.Количество();
	
	Если КоличествоСтраницИстории = 0 Тогда
		Возврат;
	КонецЕсли;
	
	мОсновнаяПанель.ТекущаяСтраница = мМассивПереходовПоСтраницам[КоличествоСтраницИстории - 1];
	
	мМассивПереходовПоСтраницам.Удалить(КоличествоСтраницИстории - 1);
	
КонецПроцедуры


// НАЗАД
Процедура ОсновныеДействияФормыНазад(Кнопка)
	
	ВыполнитьПереходНазад();
	
КонецПроцедуры

//ДАЛЕЕ
Процедура ОсновныеДействияФормыДалее(Кнопка)
	
	ВыполнитьПереходДалее();
	
КонецПроцедуры

Процедура ПанельГлавнаяПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = 1 Тогда
		
		ОпределитьДоступностьУстановкиФлажковПКО(ЭтаФорма, ЭтотОбъект);
		
	ИначеЕсли ТекущаяСтраница = 2 Тогда
	
		АктивизироватьСтраницуСобытия(ЭтотОбъект, мМассивОбработчиков, ЭлементыФормы.ПанельАлгоритмы, ЭлементыФормы);
		
	КонецЕсли;
	
	ОпределитьДоступностьКнопокПереходов();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыПомощь(Кнопка)
	
	ПоказатьИнформациюОБыстромОсвоении("ПодробноеЗнакомствоСПравиламиКонвертацииОбъектов");
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	УстановитьАлгоритмыПередЗаписью(ЭтаФорма, ЭтотОбъект, мМассивОбработчиков);
	
КонецПроцедуры

// Процедура - обработчик события "ПриСменеСтраницы" панели закладок.
//
Процедура ПанельАлгоритмыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ОбновитьПиктограммыАлгоритмов(мМассивОбработчиков, ЭлементыФормы.ПанельАлгоритмы, ЭлементыФормы);
	
	ЗаполнитьИмяОбработчика(Элемент.ТекущаяСтраница);
	
КонецПроцедуры // ПанельАлгоритмыПриСменеСтраницы()

Процедура ГлавнаяКоманднаяПанельИнформацияПоОбработчикам(Кнопка)
	
	// вывод информации по обработчикам событий
	ОткрытьОписаниеОбработчиковСобытий("ПравилаКонвертацииОбъектов", ЭлементыФормы.ПанельАлгоритмы.ТекущаяСтраница.Имя, ЭтаФорма);
	
КонецПроцедуры

Процедура ОпределитьДоступностьКнопокПереходов()
	
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Назад.Доступность = (мМассивПереходовПоСтраницам.Количество() > 0);
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Далее.Доступность = ЭлементыФормы.ПанельГлавная.ТекущаяСтраница <> ЭлементыФормы.ПанельГлавная.Страницы.События;
	
КонецПроцедуры

Процедура НеЗапоминатьВыгруженныеПриИзменении(Элемент)
	
	ОпределитьДоступностьУстановкиФлажковПКО(ЭтаФорма, ЭтотОбъект);
	
КонецПроцедуры

Процедура СинхронизироватьПоИдентификаторамПриИзменении(Элемент)
	
	ОпределитьДоступностьУстановкиФлажковПКО(ЭтаФорма, ЭтотОбъект);
	
КонецПроцедуры

Процедура ЗаполнитьИмяОбработчика(ТекущаяСтраница)
	
	ИмяСобытия = Сред(ТекущаяСтраница.Имя, 9);
	ИмяОбработчика = "ПКО_[КодПКО]_[ИмяСобытия]";
	ИмяОбработчика = СтрЗаменить(ИмяОбработчика, "[КодПКО]", СокрЛП(ЭтотОбъект.Код));
	ИмяОбработчика = СтрЗаменить(ИмяОбработчика, "[ИмяСобытия]", ИмяСобытия);
	ЭлементыФормы["ИмяОбработчика"+ИмяСобытия].Значение = ИмяОбработчика;
	
КонецПроцедуры

Процедура ДействияФормыСкопироватьОбработчикиВыгрузки(Кнопка)
	
	ВыгрузитьКодОбработчиков(ЭтотОбъект.Владелец, Истина, ЭтотОбъект.Ссылка);
	
КонецПроцедуры

Процедура ДействияФормыСкопироватьОбработчикиЗагрузки(Кнопка)
	
	ВыгрузитьКодОбработчиков(ЭтотОбъект.Владелец, Ложь, ЭтотОбъект.Ссылка);
	
КонецПроцедуры

Процедура ДействияФормыВставитьОбработчики(Кнопка)
	
	ЗагрузитьКодОбработчиков(ЭтотОбъект.Владелец, ЭтаФорма, мМассивОбработчиков);
	
КонецПроцедуры

мОсновнаяПанель =  ЭлементыФормы.ПанельГлавная;
мМассивПереходовПоСтраницам = Новый Массив();

мМассивОбработчиков = ПолучитьМассивИменОбработчиков("ПравилаКонвертацииОбъектов");
