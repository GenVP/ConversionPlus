
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	Если Не ЭтоНовый() Тогда
		ИмяФайлаОбработкиТекущегоПользователя = РаботаСФайлами.ПолучитьИмяФайла(Ссылка, глТекущийПользователь);
	Иначе
		ИспользуетсяПриВыгрузке = Истина;
	КонецЕсли; 
	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриЗаписи" формы.
//
Процедура ПриЗаписи(Отказ)

	РаботаСФайлами.УстановитьИмяФайла(Ссылка, глТекущийПользователь, ИмяФайлаОбработкиТекущегоПользователя);

КонецПроцедуры // ПриЗаписи()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ ДЕЙСТВИЙ КОМАНДНОй ПАНЕЛИ ФОРМЫ

Процедура ДействияФормыСохранитьВФайл(Кнопка)

	ДиалогВыбораФайла =	Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	
	ДиалогВыбораФайла.Фильтр                      =	"Файл обработки (*.epf)|*.epf";
	ДиалогВыбораФайла.Заголовок                   =	"Выберите файл сохранения обработки";
	ДиалогВыбораФайла.ПредварительныйПросмотр     =	Ложь;
	ДиалогВыбораФайла.Расширение                  =	"epf";
	ДиалогВыбораФайла.ИндексФильтра               =	0;
	ДиалогВыбораФайла.ПолноеИмяФайла              =	ИмяФайлаОбработкиТекущегоПользователя;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла =	Ложь;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ИмяФайлаОбработки = ДиалогВыбораФайла.ПолноеИмяФайла;
		ХранилищеОбработки.Получить().Записать(ИмяФайлаОбработки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыЗагрузитьИзФайла(Кнопка)
	
	ИмяФайлаОбработкиТекущегоПользователяНачалоВыбора("", Истина);
	
КонецПроцедуры

Процедура ИмяФайлаОбработкиТекущегоПользователяНачалоВыбора(Элемент, СтандартнаяОбработка)

	ДиалогВыбораФайла =	Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Фильтр                      =	"Файл обработки (*.epf)|*.epf";
	ДиалогВыбораФайла.Заголовок                   =	"Выберите файл обработки";
	ДиалогВыбораФайла.ПредварительныйПросмотр     =	Ложь;
	ДиалогВыбораФайла.Расширение                  =	"epf";
	ДиалогВыбораФайла.ИндексФильтра               =	0;
	ДиалогВыбораФайла.ПолноеИмяФайла              =	ИмяФайлаОбработкиТекущегоПользователя;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла =	Истина;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ИмяФайлаОбработки = ДиалогВыбораФайла.ПолноеИмяФайла;
		Файл              = Новый Файл(ИмяФайлаОбработки);
		Если Файл.Существует() Тогда
			ХранилищеОбработки = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ИмяФайлаОбработки), Новый СжатиеДанных(9));
			Если ПустаяСтрока(Код) Тогда
				Код = Файл.ИмяБезРасширения;
			КонецЕсли;
			Если ПустаяСтрока(Наименование) Тогда
				Наименование = СокрЛП(Код);
			КонецЕсли;
			ИмяФайлаОбработкиТекущегоПользователя = ИмяФайлаОбработки;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры







