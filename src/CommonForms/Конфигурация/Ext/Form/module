////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	СохраненнаяКонфигурация = ВосстановитьЗначение("Конфигурация.Конфигурация");
	СохраненныйОбъект = ВосстановитьЗначение("Конфигурация.Объекты");
	СохраненныйРеквизит = ВосстановитьЗначение("Конфигурация.Свойства");
	СохраненноеЗначение= ВосстановитьЗначение("Конфигурация.Значения");
	
	Если НЕ ЗначениеЗаполнено(Конфигурация) Тогда
		
		ОбъектСохраненнойКонфигурации = Неопределено;
			
		Если ЗначениеЗаполнено(СохраненнаяКонфигурация) Тогда
			
			Попытка
				ОбъектСохраненнойКонфигурации = СохраненнаяКонфигурация.ПолучитьОбъект();
			Исключение
				ОбъектСохраненнойКонфигурации = Неопределено;				
			КонецПопытки;
			
		КонецЕсли;
		
		// выбираем первую из имеющихся конфигураций
		Если ОбъектСохраненнойКонфигурации = Неопределено Тогда
				
			Выборка = Справочники.Конфигурации.Выбрать();	
				
			Если Выборка.Следующий() Тогда
					
				Конфигурация = Выборка.Ссылка;
					
			КонецЕсли;				
			
		Иначе
			
			Конфигурация = СохраненнаяКонфигурация;
				
		КонецЕсли;		
				
	Конецесли;
	
	Свойства.Порядок.Установить("Код");
	Значения.Порядок.Установить("Код");
	
	Если Конфигурация = СохраненнаяКонфигурация
		И ЗначениеЗаполнено(СохраненныйОбъект) Тогда
		
		ЭлементыФормы.Объекты.ТекущаяСтрока = СохраненныйОбъект;
		
		Если ЗначениеЗаполнено(СохраненноеЗначение) Тогда
			
			ЭлементыФормы.Значения.ТекущаяСтрока = СохраненноеЗначение;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СохраненныйРеквизит) Тогда
			
			ЭлементыФормы.Свойства.ТекущаяСтрока = СохраненныйРеквизит;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередОткрытием()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНОЙ ПАНЕЛИ ФОРМЫ

Процедура ОбъектыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОбъектыМетаданныхПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки);
	
КонецПроцедуры // ОбъектыПриВыводеСтроки()

Процедура СвойстваПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки = Неопределено 
		ИЛИ ДанныеСтроки.Ссылка.ПометкаУдаления = Истина Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ДанныеСтроки.Вид = Перечисления.ВидыСвойств.НаборДвиженийРегистраСведений Тогда
		
		ОформлениеСтроки.Ячейки.Картинка.УстановитьКартинку(БиблиотекаКартинок.РегистрСведений);
		
	ИначеЕсли ДанныеСтроки.Вид = Перечисления.ВидыСвойств.НаборДвиженийРегистраНакопления Тогда
		
		ОформлениеСтроки.Ячейки.Картинка.УстановитьКартинку(БиблиотекаКартинок.РегистрНакопления);
		
	ИначеЕсли ДанныеСтроки.Вид = Перечисления.ВидыСвойств.НаборДвиженийРегистраБухгалтерии Тогда
		
		ОформлениеСтроки.Ячейки.Картинка.УстановитьКартинку(БиблиотекаКартинок.РегистрБухгалтерии);
		
	ИначеЕсли ДанныеСтроки.Вид = Перечисления.ВидыСвойств.НаборДвиженийРегистраРасчета Тогда
		
		ОформлениеСтроки.Ячейки.Картинка.УстановитьКартинку(БиблиотекаКартинок.РегистрРасчета);
		
	КонецЕсли;
	
КонецПроцедуры // СвойстваПриВыводеСтроки()

Процедура ЗагрузитьОписаниеКонфигурацииНажатие(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Конфигурация) Тогда
		
		СообщитьОбОшибке("Не выбрана конфигурация");
		Возврат;
		
	КонецЕсли;
	
	// загружаем описание конфигурации из файла
	Обработка = Обработки.ЗагрузкаСтруктурыМетаданных.Создать();
	Обработка.Конфигурация  = Конфигурация;
	Обработка.ИмяФайлаЗагрузки = РаботаСФайлами.ПолучитьИмяФайла(Конфигурация, глТекущийПользователь);	
	
	ФормаОбработки = Обработка.ПолучитьФорму();
	ФормаОбработки.Открыть();
	
КонецПроцедуры

Процедура СохранитьЗначениеОбъектаТаблицы(ИмяСохранения, ТекщиеДанные)
	
	Если ТекщиеДанные = Неопределено Тогда
		СохранитьЗначение(ИмяСохранения, Неопределено);
	Иначе
		СохранитьЗначение(ИмяСохранения, ТекщиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СохранитьЗначение("Конфигурация.Конфигурация", Конфигурация);
	СохранитьЗначениеОбъектаТаблицы("Конфигурация.Объекты", ЭлементыФормы.Объекты.ТекущиеДанные);
	СохранитьЗначениеОбъектаТаблицы("Конфигурация.Свойства", ЭлементыФормы.Свойства.ТекущиеДанные);
	СохранитьЗначениеОбъектаТаблицы("Конфигурация.Значения", ЭлементыФормы.Значения.ТекущиеДанные);
	
КонецПроцедуры

Функция ПолучитьКоличествоОбъектовЗначенийПоВладельцу(ИмяСправочника, Владелец)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	Количество(Различные Спр.Ссылка) КАК Количество
	               |ИЗ
	               |	Справочник." + ИмяСправочника + " КАК Спр
	               |ГДЕ
	               |	Спр.Владелец = &Владелец";
				   
	Запрос.УстановитьПараметр("Владелец", Владелец);			   
				   
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Если Таблица.Количество() = 0 Тогда
		Возврат 0;
	Иначе
		Возврат Таблица[0].Количество;
	КонецЕсли;
	
КонецФункции

Процедура ОбъектыПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = ЭлементыФормы.Объекты.ТекущиеДанные;
		
	Если ТекущаяСтрока = Неопределено Тогда
		
		КоличествоСвойств = 0;
		КоличествоЗначений = 0;
						
	Иначе	
		
		КоличествоСвойств = ПолучитьКоличествоОбъектовЗначенийПоВладельцу("Свойства", ТекущаяСтрока.Ссылка);
		КоличествоЗначений = ПолучитьКоличествоОбъектовЗначенийПоВладельцу("Значения", ТекущаяСтрока.Ссылка);
		
	КонецЕсли;
	
	
	Если КоличествоСвойств = 0
		И КоличествоЗначений = 0 Тогда
		
		ЭлементыФормы.ПанельСвойстваЗначения.Страницы.Свойства.Заголовок = "Свойства"; 
		ЭлементыФормы.ПанельСвойстваЗначения.Страницы.Значения.Заголовок = "Значения";
	
	    Возврат;
		
	КонецЕсли;

	
	ЭлементыФормы.ПанельСвойстваЗначения.Страницы.Свойства.Заголовок = "Свойства (" + Строка(КоличествоСвойств) + ")"; 
	ЭлементыФормы.ПанельСвойстваЗначения.Страницы.Значения.Заголовок = "Значения (" + Строка(КоличествоЗначений) + ")"; 
	
	Если КоличествоСвойств > 0 И КоличествоЗначений > 0
		ИЛИ (КоличествоСвойств = 0 И КоличествоЗначений = 0) Тогда
		
		Возврат;
		
	КонецЕсли;
		
	Если КоличествоСвойств > 0 Тогда
		ЭлементыФормы.ПанельСвойстваЗначения.ТекущаяСтраница =  ЭлементыФормы.ПанельСвойстваЗначения.Страницы.Свойства;
	КонецЕсли;
	
	Если КоличествоЗначений > 0 Тогда
		ЭлементыФормы.ПанельСвойстваЗначения.ТекущаяСтраница =  ЭлементыФормы.ПанельСвойстваЗначения.Страницы.Значения;
	КонецЕсли;
	
КонецПроцедуры

Объекты.Колонки.Добавить("ПометкаУдаления");
Объекты.Колонки.Добавить("Тип");
