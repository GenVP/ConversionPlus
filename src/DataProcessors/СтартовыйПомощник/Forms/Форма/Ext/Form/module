
Перем мМассивПереходовПоСтраницам;
Перем мОсновнаяПанель;

Процедура ПроверитьСтраницаКонечная()
	
	Если (ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.СоответствиеОбъектов)
		ИЛИ (ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.МодификацияСуществующегоОбмена)
		ИЛИ (ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.ВыборДействияПользователем
		И ДействиеПользователя >= 2) Тогда
		СтраницаКонечная = Истина;
	Иначе
		СтраницаКонечная = Ложь;
	КонецЕсли;
	
	ЭлементыФормы.ПанельКнопок.ТекущаяСтраница = ?(СтраницаКонечная, ЭлементыФормы.ПанельКнопок.Страницы.Готово, ЭлементыФормы.ПанельКнопок.Страницы.Далее);
	ЭлементыФормы.ПанельКартинки.ТекущаяСтраница = ?(СтраницаКонечная, ЭлементыФормы.ПанельКартинки.Страницы.СтраницаГотово, ЭлементыФормы.ПанельКартинки.Страницы.СтраницаНастройки);
	
КонецПроцедуры

Процедура ВыполнитьПереходДалее()
	
	Если ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.Этап1 Тогда
		
		мМассивПереходовПоСтраницам.Добавить(мОсновнаяПанель.ТекущаяСтраница);
		ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.ВыборДействияПользователем;
		
	ИначеЕсли ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.ВыборДействияПользователем Тогда
		
		мМассивПереходовПоСтраницам.Добавить(мОсновнаяПанель.ТекущаяСтраница);
		
		Если ДействиеПользователя = 0 Тогда
			
			ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.СозданиеНовогоОбмена;
			
		ИначеЕсли ДействиеПользователя = 1 Тогда
			
			ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.МодификацияСуществующегоОбмена;
			
		ИначеЕсли ДействиеПользователя = 2 Тогда
			
			// загрузить правила обмена из файла
			Обработки.ЗагрузкаКонвертации.ПолучитьФорму().Открыть();
			Закрыть();
			
		ИначеЕсли ДействиеПользователя = 3 Тогда
			
			Обработки.ЗагрузкаСтруктурыМетаданных.ПолучитьФорму().Открыть();
			Закрыть();
			
		КонецЕсли;	
		
	ИначеЕсли ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.СозданиеНовогоОбмена Тогда
		
		Если НЕ ЗначениеЗаполнено(КонфигурацияИсточник) Тогда
			
			Предупреждение("Не выбрана конфигурация - источник");
			Возврат;
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(КонфигурацияПриемник) Тогда
			
			Предупреждение("Не выбрана конфигурация - приемник");
			Возврат;
			
		КонецЕсли;
		
		мМассивПереходовПоСтраницам.Добавить(мОсновнаяПанель.ТекущаяСтраница);
		
		ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.СоответствиеОбъектов;
		
	ИначеЕсли ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.СоответствиеОбъектов Тогда
		
		мМассивПереходовПоСтраницам.Добавить(мОсновнаяПанель.ТекущаяСтраница);
		
		ОбъектКонвертации = Справочники.Конвертации.СоздатьЭлемент();
		ОбъектКонвертации.Источник = КонфигурацияИсточник;
		ОбъектКонвертации.Приемник = КонфигурацияПриемник;
		ОбъектКонвертации.УстановитьНовыйКод();
		ОбъектКонвертации.Записать();
		
		Конвертация = ОбъектКонвертации.Ссылка;
		РезультатСоздания = Истина;
		
		Если ТипНастройкиПравилОбмена = 0 Тогда
			
			СписокСозданныхПравил = Новый СписокЗначений;
			РезультатСоздания = АвтоСозданиеВсехВозможныхПравилОбмена(Конвертация, Ложь, СписокСозданныхПравил);
			
			Если РезультатСоздания Тогда
				
				ФормаПравилОбмена = ПолучитьОбщуюФорму("ПравилаОбмена");	
				ФормаПравилОбмена.Конвертация = Конвертация;
				ФормаПравилОбмена.Открыть();
				ФормаПравилОбмена.ВыполнитьДействияПослеУспешногоСозданияВсехВозможныйПравилОбмена(СписокСозданныхПравил);
				
			КонецЕсли;
			
		ИначеЕсли ТипНастройкиПравилОбмена = 1 Тогда
			
			ФормаПравилОбмена = ПолучитьОбщуюФорму("ПравилаОбмена");	
			ФормаПравилОбмена.Конвертация = Конвертация;
			ФормаПравилОбмена.Открыть();
			ФормаПравилОбмена.ПроизвестиСинхронизациюОбъектов();
			
		ИначеЕсли ТипНастройкиПравилОбмена = 2 Тогда
			
			ФормаПравилОбмена = ПолучитьОбщуюФорму("ПравилаОбмена");	
			ФормаПравилОбмена.Конвертация = Конвертация;
			ФормаПравилОбмена.Открыть();	
			
			НовыйЭлементПКО = Справочники.ПравилаКонвертацииОбъектов.СоздатьЭлемент();
			НовыйЭлементПКО.Владелец = Конвертация;
			ФормаПКО = НовыйЭлементПКО.ПолучитьФорму(, ФормаПравилОбмена.ЭлементыФормы.ПравилаКонвертацииОбъектов);
	
			ФормаПКО.Открыть();		
						
		КонецЕсли;
		
		Если РезультатСоздания Тогда
			Закрыть();
		КонецЕсли;
		
	ИначеЕсли ЭлементыФормы.ПанельЭтапов.ТекущаяСтраница = ЭлементыФормы.ПанельЭтапов.Страницы.МодификацияСуществующегоОбмена Тогда
		
		Если НЕ ЗначениеЗаполнено(КонвертацияДляИзменения) Тогда
			
			Предупреждение("Не выбраны правила обмена, подлежащие изменению");
			Возврат;
			
		КонецЕсли;
		
		ФормаПравилОбмена = ПолучитьОбщуюФорму("ПравилаОбмена");	
		ФормаПравилОбмена.Конвертация = КонвертацияДляИзменения;
		ФормаПравилОбмена.Открыть();
		
		Закрыть();
				
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьПереходНазад()
	
	КоличествоСтраницИстории = мМассивПереходовПоСтраницам.Количество();
	
	Если КоличествоСтраницИстории = 0 Тогда
		Возврат;
	КонецЕсли;
	
	мОсновнаяПанель.ТекущаяСтраница = мМассивПереходовПоСтраницам[КоличествоСтраницИстории - 1];
	
	мМассивПереходовПоСтраницам.Удалить(КоличествоСтраницИстории - 1);
	
КонецПроцедуры

Процедура НадписьВпередНажатие(Элемент)
	
	ВыполнитьПереходДалее();	
	
КонецПроцедуры

Процедура НадписьНазадНажатие(Элемент)
	
	ВыполнитьПереходНазад();
	
КонецПроцедуры

Процедура ПодробнееНажатие(Элемент)
	
	ПоказатьИнформациюОБыстромОсвоении("ВыгрузкаИЗагрузкаМетаданных");	
	
КонецПроцедуры

Процедура НоваяКонфигурацияНажатие(Элемент)
	
	// загружаем описание конфигурации из файла
	Обработка = Обработки.ЗагрузкаСтруктурыМетаданных.Создать();
		
	ФормаОбработки = Обработка.ПолучитьФорму(, ЭлементыФормы.КонфигурацияИсточник, 1);
	ФормаОбработки.Открыть();	
	
КонецПроцедуры

Процедура НоваяКонфигурация1Нажатие(Элемент)
	
	// загружаем описание конфигурации из файла
	Обработка = Обработки.ЗагрузкаСтруктурыМетаданных.Создать();
		
	ФормаОбработки = Обработка.ПолучитьФорму(, ЭлементыФормы.КонфигурацияПриемник, 1);
	ФормаОбработки.Открыть();
	
КонецПроцедуры

Процедура ПанельЭтаповПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ПроверитьСтраницаКонечная();
	
КонецПроцедуры

Процедура ДействиеПользователяПриИзменении(Элемент)
	
	ПроверитьСтраницаКонечная();
	
КонецПроцедуры

Процедура Подробнее1Нажатие(Элемент)
		
	Обработка = Обработки.ЗагрузкаСтруктурыМетаданных.Создать();
		
	ФормаОбработки = Обработка.ПолучитьФорму(, ЭлементыФормы.КонфигурацияПриемник, 2);
	ФормаОбработки.Открыть();	
	
КонецПроцедуры

Процедура ОпределитьТипДействияПользователяПоУмолчанию()
	
	// проверка наличие конфигураций
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ Количество(*) Как Количество
	               |	
	               |ИЗ
	               |	Справочник.Конфигурации КАК Конфигурации
				   |ГДЕ
				   |	Конфигурации.ПометкаУдаления = Ложь";
				   
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	НаличиеКонфигураций = ТаблицаРезультата[0].Количество <> NULL
		И ТаблицаРезультата[0].Количество > 0;
		
	Если Не НаличиеКонфигураций Тогда
		ДействиеПользователя = 3;
		Возврат;
	КонецЕсли;
	
	// проверка наличия правил обмена
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ Количество(*) Как Количество
	               |	
	               |ИЗ
	               |	Справочник.Конвертации КАК Конвертации
				   |ГДЕ
				   |	Конвертации.ПометкаУдаления = Ложь";
				   
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	НаличиеКонвертации = ТаблицаРезультата[0].Количество <> NULL
		И ТаблицаРезультата[0].Количество > 0;
		
	Если Не НаличиеКонвертации Тогда
		ДействиеПользователя = 2;
		Возврат;
	КонецЕсли;
	
	ДействиеПользователя = 1;	
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ОпределитьТипДействияПользователяПоУмолчанию();	
	
КонецПроцедуры


мОсновнаяПанель =  ЭлементыФормы.ПанельЭтапов;
мМассивПереходовПоСтраницам = Новый Массив();
