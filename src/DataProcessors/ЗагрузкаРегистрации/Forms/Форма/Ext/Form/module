
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если ПустаяСтрока(РежимЗагрузкиРегистрации) Тогда
		РежимЗагрузкиРегистрации = "Создать";
	КонецЕсли;
	
	Если ПустаяСтрока(РежимЗагрузкиКонфигурации) Тогда
		РежимЗагрузкиКонфигурации = "Создать";
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ ФОРМЫ

Процедура ИмяФайлаПравилНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	Фильтр     = "Файл правил (*.xml)|*.xml|Архивный файл правил (*.zip)|*.zip";
	Расширение = "xml";
	
	РаботаСФайлами.глВыборФайла(Элемент, Истина, Фильтр, Расширение, "Выберите имя файла с правилами регистрации");
	
	ИмяФайлаПравилПриИзменении(Элемент);
	
КонецПроцедуры

Процедура ИмяФайлаПравилОткрытие(Элемент, СтандартнаяОбработка)
	
	РаботаСФайлами.глОткрытьФайл(ИмяФайлаПравил, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура РежимЗагрузкиРегистрации_1ПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура РежимЗагрузкиКонфигурации_1ПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура РегистрацияПриИзменении(Элемент)
	
	ПолучитьЗначениеКонфигурации();
	
КонецПроцедуры

Процедура ИмяФайлаПравилПриИзменении(Элемент)
	
	Если ПустаяСтрока(ИмяФайлаПравил) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = ПолучитьДанныеРегистрацииИзФайла();
	
	Если ДанныеФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РегистрацияИзФайла = Справочники.Регистрации.ПолучитьСсылку(Новый УникальныйИдентификатор(ДанныеФайла.УникальныйИдентификаторРегистрации));
	
	РегистрацияОбъект = РегистрацияИзФайла.ПолучитьОбъект();
	
	НаименованиеНовойРегистрации = ДанныеФайла.НаименованиеРегистрации;
	НаименованиеНовойКонфигурации = ДанныеФайла.НаименованиеКонфигурации;
	
	Если РегистрацияОбъект = Неопределено Тогда
		
		РежимЗагрузкиРегистрации = "Создать";
		РежимЗагрузкиКонфигурации = "Создать";
		
	Иначе
		
		РежимЗагрузкиРегистрации = "Обновить";
		РежимЗагрузкиКонфигурации = "Обновить";
		
		Регистрация = РегистрацияИзФайла;
		Конфигурация = Регистрация.Конфигурация;
		
		Если Конфигурация.Пустая() Тогда
			
			РежимЗагрузкиКонфигурации = "Создать";
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ КОМАНДНОЙ ПАНЕЛИ ОсновныеДействияФормы

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	// выполняем процесс загрузки регистрации
	ВыполнитьЗагрузкуРегистрации();
	
	СтрокаСообщения = ?(ФлагОшибки, "В процессе загрузки правил были обнаружены ошибки! Загрузка не выполнена.",
	                                "Загрузка правил регистрации успешно завершена!");
	
	Предупреждение(СтрокаСообщения);
	
	Если Не ФлагОшибки Тогда
		
		Форма = ПолучитьОбщуюФорму("ПравилаРегистрации");
		Форма.Регистрация = Регистрация;
		Форма.Открыть();
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура УстановитьВидимость()
	
	// текущая страница панели регистрации
	ЭлементыФормы.ПанельРегистрация.ТекущаяСтраница  = ЭлементыФормы.ПанельРегистрация.Страницы["Страница" + РежимЗагрузкиРегистрации];
	
	// текущая страница панели конфигурации
	ЭлементыФормы.ПанельКонфигурация.ТекущаяСтраница = ЭлементыФормы.ПанельКонфигурация.Страницы["Страница" + РежимЗагрузкиКонфигурации];
	
КонецПроцедуры

Процедура ПолучитьЗначениеКонфигурации()
	
	Конфигурация = Регистрация.Конфигурация;
	
	РежимЗагрузкиКонфигурации = "Обновить";
	
	УстановитьВидимость();
	
КонецПроцедуры
